plugins {
    id "kotlin-kapt"
    id "com.android.application"
    id "dagger.hilt.android.plugin"
    id "org.jetbrains.kotlin.android"
}

android {
    namespace "com.krishnaZyala.faceRecognition"
    compileSdk 33

    defaultConfig {
        applicationId "com.krishnaZyala.faceRecognition"
        minSdk 24
        targetSdk 33
        versionCode 1000
        versionName "1.0.00"
        vectorDrawables { useSupportLibrary true }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildFeatures { compose true }
    aaptOptions { noCompress "tflite" }
    kotlinOptions { jvmTarget = "17" }
    composeOptions { kotlinCompilerExtensionVersion "1.4.7" }
    packagingOptions { resources { excludes += "/META-INF/{AL2.0,LGPL2.1}" } }
}

dependencies {
    /* Android Core */
    implementation "androidx.core:core-ktx:1.10.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.1"
    implementation platform("org.jetbrains.kotlin:kotlin-bom:1.8.21")
    /* Jetpack Compose */
    implementation "androidx.activity:activity-compose:1.7.2"
    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation platform('androidx.compose:compose-bom:2023.05.01')
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.ui:ui-graphics"
    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.ui:ui-tooling-preview"
    /* Core Testing */
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    /* Jetpack Compose Testing */
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    androidTestImplementation platform("androidx.compose:compose-bom:2023.05.01")
    androidTestImplementation "androidx.compose.ui:ui-test-junit4"
    debugImplementation "androidx.compose.ui:ui-tooling"
    debugImplementation "androidx.compose.ui:ui-test-manifest"
    /*JSON*/
    implementation 'com.google.code.gson:gson:2.10.1'
    /* Dagger Hilt */
    def dagger_version = "2.46.1"
    def hilt_version = "1.0.0"
    kapt "androidx.hilt:hilt-compiler:${hilt_version}"
    kapt "com.google.dagger:hilt-compiler:${dagger_version}"
    implementation "com.google.dagger:hilt-android:${dagger_version}"
    implementation "androidx.hilt:hilt-navigation-compose:$hilt_version"
    /* Camera X */
    def camerax_version = "1.2.3"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-extensions:${camerax_version}"
    /* Room Database */
    def room_version = "2.5.1"
    kapt "androidx.room:room-compiler:${room_version}"
    implementation "androidx.room:room-ktx:${room_version}"
    implementation "androidx.room:room-runtime:${room_version}"
    annotationProcessor "androidx.room:room-compiler:${room_version}"
    /* ML Kit */
    implementation "com.google.mlkit:face-detection:16.1.5"
    implementation "com.google.android.gms:play-services-mlkit-face-detection:17.1.0"
    /* TensorFlow Lite */
    implementation "org.tensorflow:tensorflow-lite:2.12.0"
    implementation "org.tensorflow:tensorflow-lite-gpu:2.12.0"
    implementation "org.tensorflow:tensorflow-lite-support:0.4.3"
    implementation "org.tensorflow:tensorflow-lite-task-vision:0.4.3"
}